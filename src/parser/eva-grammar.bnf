// ----------------------------------------
// Lexical grammar (tokens)

%lex

%%

\s+                /* skip whitespace */ return "";
\"[^\"]*\"         return "STRING"
[\-]?\d+           return "NUMBER"
'true'|'false'     return "BOOLEAN"
'null'             return "NULL"
'begin'            return "BEGIN"
'var'              return "VAR"
'if'               return "IF"
'while'            return "WHILE"
'set'              return "ASSIGN"
[+\-*/=<>]+        return "OPERATOR"
[\w]+              return "IDENTIFIER"

/lex

// ----------------------------------------
// Syntactic grammar (BNF)

%{

#[derive(Debug,PartialEq,Clone)]
pub enum Expr {
  Literal(Value),
  BinaryExpression(String, Box<Expr>, Box<Expr>),
  VariableDeclaration(String, Box<Expr>),
  Identifier(String),
  BlockStatement(Vec<Expr>),
  Assignment(String, Box<Expr>),
  IfExpression(Box<Expr>, Box<Expr>, Box<Expr>),
  WhileStatement(Box<Expr>, Box<Expr>)
}

#[derive(Debug,PartialEq,Clone)]
pub enum Value {
  Int(isize),
  Str(String),
  Null,
  Boolean(bool)
}

type TResult = Expr;

%}

%%

Expression
    : Literal { 
      |$1: Value| -> Expr;
      $$ = Expr::Literal($1) 
    }
    | IDENTIFIER {
      || -> Expr;
      $$ = Expr::Identifier(yytext.to_string())
    }
    | SExpr { $$ = $1 }
    ;

Literal
    : NUMBER {
      || -> Value;
      $$ = Value::Int(yytext.parse::<isize>().unwrap())
    }
    | STRING {
      || -> Value;
      $$ = Value::Str(yytext.to_string())
    }
    | BOOLEAN {
      || -> Value;
      $$ = Value::Boolean(yytext == "true")
    }
    | NULL {
      || -> Value;
      $$ = Value::Null
    }
    ;

ExpressionsList
  : ExpressionsList Expression {
    |$1: Vec<Expr>, $2: Expr | -> Vec<Expr>;
    $1.push($2);
    $$ = $1
  }
  | /* empty */ {
    || -> Vec<Expr>; 
    $$ = Vec::new() 
  }
  ;

SExpr
  : '(' BEGIN ExpressionsList ')' {
    |$3: Vec<Expr>| -> Expr;
    $$ = Expr::BlockStatement($3)
  }
  | '(' VAR IDENTIFIER Expression ')' {
    |$3: String, $4: Expr| -> Expr;
    $$ = Expr::VariableDeclaration($3, Box::new($4))
  }
  | '(' IF Expression Expression Expression ')' {
    |$3: Expr, $4: Expr, $5: Expr| -> Expr;
    $$ = Expr::IfExpression(Box::new($3), Box::new($4), Box::new($5))
  }
  | '(' WHILE Expression Expression ')' {
    |$3: Expr, $4: Expr| -> Expr;
    $$ = Expr::WhileStatement(Box::new($3), Box::new($4))
  }
  | '(' ASSIGN IDENTIFIER Expression ')' {
    |$3: String, $4: Expr| -> Expr;
    $$ = Expr::Assignment($3, Box::new($4))
  }
  | '(' OPERATOR Expression Expression ')' {
    |$3: String, $4: Expr, $5: Expr| -> Expr;
    $$ = Expr::BinaryExpression($3, Box::new($4), Box::new($5))
  }
  ;

